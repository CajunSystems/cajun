plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.cajunsystems'
version = project.findProperty('cajunVersion') ?: '0.4.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

sourceSets {
    main {
        java {
            exclude 'module-info.java'
            exclude 'com/cajunsystems/cluster/impl/ClusterFactory.java'
        }
    }
}

// Task to create a bundle for Central Portal upload
tasks.register('createCentralBundle', Zip) {
    from layout.buildDirectory.dir('repo')
    archiveFileName = "cajun-cluster-${version}-bundle.zip"
    destinationDirectory = layout.buildDirectory.dir('distributions')
    dependsOn 'publishMavenJavaPublicationToCentralPortalRepository'
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

tasks.withType(Javadoc).configureEach {
    options.addBooleanOption('-enable-preview', true)
    options.addStringOption('source', '21')
}

dependencies {
    // Depends on core
    api project(':cajun-core')

    // Etcd for leader election and coordination
    implementation 'io.etcd:jetcd-core:0.8.4'

    // gRPC stubs for cluster messaging
    implementation 'io.grpc:grpc-stub:1.62.2'

    // Testing
    testImplementation libs.junit.jupiter
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.5.16'

    // Override vulnerable transitive dependencies
    constraints {
        implementation('io.netty:netty-codec-http2:4.1.116.Final') {
            because 'fixes CVE-2023-44487'
        }
        implementation('io.netty:netty-codec-http:4.1.116.Final') {
            because 'fixes CVE-2024-29025'
        }
        implementation('io.netty:netty-handler:4.1.116.Final') {
            because 'fixes CVE-2025-24970'
        }
        implementation('io.netty:netty-common:4.1.116.Final') {
            because 'fixes CVE-2024-47535, CVE-2025-25193'
        }
        implementation('io.vertx:vertx-core:4.5.11') {
            because 'fixes CVE-2024-1300'
        }
    }
}

tasks.named('test') {
    jvmArgs(['--enable-preview'])
    useJUnitPlatform {
        excludeTags 'performance', 'requires-etcd'
    }
}

publishing {
    publications {
        create('mavenJava', MavenPublication) {
            from components.java
            artifactId = 'cajun-cluster'

            pom {
                name.set('Cajun Cluster')
                description.set('Clustering support for Cajun actor system')
                url.set('https://github.com/cajunsystems/cajun')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('cajunsystems')
                        name.set('CajunSystems')
                        email.set('info@cajunsystems.io')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/cajunsystems/cajun.git')
                    developerConnection.set('scm:git:ssh://github.com:cajunsystems/cajun.git')
                    url.set('https://github.com/cajunsystems/cajun')
                }
            }
        }
    }
    repositories {
        maven {
            name = 'CentralPortal'
            url = layout.buildDirectory.dir('repo')
        }
    }
}

def hasSigningCredentials = project.hasProperty('signing.keyId') &&
        project.hasProperty('signing.password') &&
        project.hasProperty('signing.secretKeyRingFile')

signing {
    required {
        gradle.taskGraph.hasTask('publishMavenJavaPublicationToCentralPortalRepository') && hasSigningCredentials
    }

    if (hasSigningCredentials) {
        sign publishing.publications.mavenJava
    } else {
        logger.lifecycle("Signing disabled for ${project.path} - missing signing properties")
    }
}
