plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.cajunsystems'
version = project.findProperty('cajunVersion') ?: '0.2.0'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
    withJavadocJar()
    withSourcesJar()
}

tasks.withType(JavaCompile).each {
    it.options.compilerArgs.add('--enable-preview')
}

tasks.withType(Javadoc) {
    options.addBooleanOption('-enable-preview', true)
    options.addStringOption('source', '21')
}

dependencies {
    // Core modules
    api project(':cajun-core')
    api project(':cajun-mailbox')
    api project(':cajun-persistence')

    // Optionally depend on cluster (users can exclude if not needed)
    compileOnly project(':cajun-cluster')

    // Testing
    testImplementation libs.junit.jupiter
    testImplementation 'org.mockito:mockito-core:5.7.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testRuntimeOnly 'ch.qos.logback:logback-classic:1.4.11'
}

tasks.named('test') {
    jvmArgs(['--enable-preview'])
    useJUnitPlatform {
        excludeTags 'performance'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifactId = 'cajun-system'

            pom {
                name.set('Cajun System')
                description.set('Core actor system runtime for Cajun')
                url.set('https://github.com/cajunsystems/cajun')

                licenses {
                    license {
                        name.set('MIT License')
                        url.set('https://opensource.org/licenses/MIT')
                    }
                }

                developers {
                    developer {
                        id.set('cajunsystems')
                        name.set('CajunSystems')
                        email.set('info@cajunsystems.io')
                    }
                }

                scm {
                    connection.set('scm:git:git://github.com/cajunsystems/cajun.git')
                    developerConnection.set('scm:git:ssh://github.com:cajunsystems/cajun.git')
                    url.set('https://github.com/cajunsystems/cajun')
                }
            }
        }
    }
    repositories {
        maven {
            name = 'CentralPortal'
            url = layout.buildDirectory.dir('repo')
        }
    }
}

def hasSigningCredentials = project.hasProperty('signing.keyId') &&
        project.hasProperty('signing.password') &&
        project.hasProperty('signing.secretKeyRingFile')

signing {
    required {
        gradle.taskGraph.hasTask('publishMavenJavaPublicationToCentralPortalRepository') && hasSigningCredentials
    }

    if (hasSigningCredentials) {
        sign publishing.publications.mavenJava
    } else {
        logger.lifecycle("Signing disabled for ${project.path} - missing signing properties")
    }
}

// Task to create a bundle for Central Portal upload
tasks.register('createCentralBundle', Zip) {
    from layout.buildDirectory.dir('repo')
    archiveFileName = "cajun-system-${version}-bundle.zip"
    destinationDirectory = layout.buildDirectory.dir('distributions')
    dependsOn 'publishMavenJavaPublicationToCentralPortalRepository'
}
